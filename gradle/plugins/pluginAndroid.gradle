android {
	namespace = project.path.substring(1).replace(":", ".").replace("-", "_")

	compileSdk = config.compileSdkVersion
	compileSdkExtension = config.compileSdkExtensionVersion
	buildToolsVersion = config.buildToolsVersion

	defaultConfig {
		minSdk = config.minSdkVersion
		targetSdk = config.targetSdkVersion

		resourceConfigurations += config.supportedLocales
		ndk.abiFilters = config.availableAbi
		vectorDrawables.useSupportLibrary = true
	}

	compileOptions.incremental = true

	lint.abortOnError = false

	buildTypes {
		debug {
			debuggable = true

			if (config.debug.obfuscationEnabled) {
				minifyEnabled = true
				crunchPngs = true
				proguardFiles(getDefaultProguardFile("proguard-android-optimize.txt"), "$rootDir/proguardRules.pro")
			} else {
				minifyEnabled = false
				crunchPngs = false
			}
		}

		release {
			debuggable = config.release.debuggable

			if (config.release.obfuscationEnabled) {
				minifyEnabled = true
				crunchPngs = true
				proguardFiles(getDefaultProguardFile("proguard-android-optimize.txt"), "$rootDir/proguardRules.pro")
				if (!config.release.debuggable) proguardFiles("$rootDir/proguardRulesLogs.pro")
			} else {
				minifyEnabled = false
				crunchPngs = false
			}
		}
	}
}